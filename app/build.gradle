buildscript {
  repositories {
    mavenCentral()
    maven { url "https://maven.fabric.io/public" }
  }

  dependencies {
    classpath "io.fabric.tools:gradle:1.22.1"
  }
}

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "io.fabric"

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionBuild = 4 // bump for dogfood builds, public betas, etc.

// From https://github.com/JakeWharton/u2020/blob/1712f903084703059b9a4316b63fa9a29b4559cd/build.gradle
def gitSha() {
  def p = "git rev-parse --short HEAD".execute([], project.rootDir)
  p.waitFor()
  if (p.exitValue() != 0) {
    throw new RuntimeException(p.errorStream.text)
  }

  return p.text.trim()
}

def gitTimestamp() {
  def p = "git log -n 1 --format=%at".execute([], rootDir)
  p.waitFor()
  if (p.exitValue() != 0) {
    throw new RuntimeException(p.errorStream.text)
  }

  return p.text.trim()
}

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.2"

  dataBinding {
    enabled = true
  }

  defaultConfig {
    applicationId "com.cws.image"

    minSdkVersion 21
    targetSdkVersion 25

    versionCode 5
    versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"

    buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""
    buildConfigField 'long', 'GIT_TIMESTAMP', "${gitTimestamp()}L"

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    debug {
      applicationIdSuffix ".debug"
    }

    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile("proguard-android.txt"),
          "proguard-rules.pro"
    }
  }

  sourceSets {
    main.java.srcDirs += "src/main/kotlin"
    test.java.srcDirs += "test/main/kotlin"
  }

  dexOptions {
    javaMaxHeapSize "8g"
  }

  packagingOptions {
    exclude "META-INF/rxjava.properties"
  }
}

repositories {
  maven { url = "https://jitpack.io" }
  maven { url = "https://maven.fabric.io/public" }
}

dependencies {
  compile fileTree(dir: "libs", include: ["*.jar"])

  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  // NOTE: The version number in "com.android.databinding:compiler:<version number>"
  // must match the version of com.android.tools.build:gradle in
  // root build.gradle > classpath "com.android.tools.build:gradle:<version number>"
  // https://code.google.com/p/android/issues/detail?id=227741

  compile("com.crashlytics.sdk.android:crashlytics:2.6.6@aar") {
    transitive = true;
  }
  compile("com.crashlytics.sdk.android:answers:1.3.11@aar") {
    transitive = true;
  }


  androidTestCompile("com.android.support.test.espresso:espresso-core:2.2.2", {
    exclude group: "com.android.support", module: "support-annotations"
  })

  ext.rx_binding_version = "2.0.0"

  compile "com.android.support:appcompat-v7:25.3.1"
  compile "com.android.support:design:25.3.1"
  compile "com.android.support:recyclerview-v7:25.3.1"
  compile "com.android.support:support-v4:25.3.1"
  compile "com.android.support.constraint:constraint-layout:1.0.2"
  compile "com.github.andrewoma.dexx:kollection:0.7"
  compile "nz.bradcampbell:paperparcel:2.0.1"
  compile "nz.bradcampbell:paperparcel-kotlin:2.0.1"
  compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.14"
  compile "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.14"
  compile "io.reactivex.rxjava2:rxjava:2.1.0"
  compile "io.reactivex.rxjava2:rxandroid:2.0.1"
  compile "com.jakewharton.rxbinding2:rxbinding-kotlin:$rx_binding_version"
  compile "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$rx_binding_version"
  compile "com.jakewharton.rxbinding2:rxbinding-design-kotlin:$rx_binding_version"
  compile "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:$rx_binding_version"
  compile "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$rx_binding_version"
  compile "com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC1"
  compile "com.squareup.picasso:picasso:2.5.2"
  compile "com.jakewharton.timber:timber:4.5.1"
  compile "com.facebook.stetho:stetho:1.5.0"
  compile "com.facebook.stetho:stetho-okhttp3:1.5.0"
  testCompile "junit:junit:4.12"
  debugCompile "com.squareup.leakcanary:leakcanary-android:1.5.1"
  kapt "com.android.databinding:compiler:2.3.2"
  kapt "nz.bradcampbell:paperparcel-compiler:2.0.1"
}
