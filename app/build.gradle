apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionBuild = 3 // bump for dogfood builds, public betas, etc.

// From https://github.com/JakeWharton/u2020/blob/1712f903084703059b9a4316b63fa9a29b4559cd/build.gradle
def gitSha() {
  def p = "git rev-parse --short HEAD".execute([], project.rootDir)
  p.waitFor()
  if (p.exitValue() != 0) {
    throw new RuntimeException(p.errorStream.text)
  }

  return p.text.trim()
}

def gitTimestamp() {
  def p = "git log -n 1 --format=%at".execute([], rootDir)
  p.waitFor()
  if (p.exitValue() != 0) {
    throw new RuntimeException(p.errorStream.text)
  }

  return p.text.trim()
}

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.1"

  dataBinding {
    enabled = true
  }

  defaultConfig {
    applicationId "com.cws.image"

    minSdkVersion 19
    targetSdkVersion 25

    versionCode 4
    versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"

    buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""
    buildConfigField 'long', 'GIT_TIMESTAMP', "${gitTimestamp()}L"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
    }
  }

  sourceSets {
    main.java.srcDirs += "src/main/kotlin"
    test.java.srcDirs += "test/main/kotlin"
  }

  dexOptions {
    javaMaxHeapSize "2g"
  }

  packagingOptions {
    exclude 'META-INF/rxjava.properties'
  }
}

repositories {
  maven { url = "https://jitpack.io" }
}

dependencies {
  compile fileTree(dir: "libs", include: ["*.jar"])

  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  kapt "com.android.databinding:compiler:2.2.0"

  compile "com.android.support:appcompat-v7:25.1.1"
  compile "com.android.support:design:25.1.1"
  compile "com.android.support:recyclerview-v7:25.1.1"
  compile "com.android.support.constraint:constraint-layout:1.0.0-beta4"

  compile "com.github.andrewoma.dexx:kollection:0.7"

  compile "io.reactivex.rxjava2:rxjava:2.0.5"
  compile "io.reactivex.rxjava2:rxandroid:2.0.1"

  compile "com.github.akarnokd:rxjava2-interop:0.8.4"

  compile "com.jakewharton.rxbinding:rxbinding-kotlin:1.0.0"
  compile "com.jakewharton.rxbinding:rxbinding-appcompat-v7-kotlin:1.0.0"
  compile "com.jakewharton.rxbinding:rxbinding-design-kotlin:1.0.0"
  compile "com.jakewharton.rxbinding:rxbinding-recyclerview-v7-kotlin:1.0.0"
  compile "com.jakewharton.rxbinding:rxbinding-support-v4-kotlin:1.0.0"

  debugCompile "com.squareup.leakcanary:leakcanary-android:1.5"

  testCompile "junit:junit:4.12"

  compile "com.facebook.stetho:stetho:1.4.2"
  compile "com.facebook.stetho:stetho-okhttp3:1.4.2"
}

kapt {
  generateStubs = true
}