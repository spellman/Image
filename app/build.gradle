apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionBuild = 3 // bump for dogfood builds, public betas, etc.

// From https://github.com/JakeWharton/u2020/blob/1712f903084703059b9a4316b63fa9a29b4559cd/build.gradle
def gitSha() {
  def p = "git rev-parse --short HEAD".execute([], project.rootDir)
  p.waitFor()
  if (p.exitValue() != 0) {
    throw new RuntimeException(p.errorStream.text)
  }

  return p.text.trim()
}

def gitTimestamp() {
  def p = "git log -n 1 --format=%at".execute([], rootDir)
  p.waitFor()
  if (p.exitValue() != 0) {
    throw new RuntimeException(p.errorStream.text)
  }

  return p.text.trim()
}

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.1"

  dataBinding {
    enabled = true
  }

  defaultConfig {
    applicationId "com.cws.image"

    minSdkVersion 19
    targetSdkVersion 25

    versionCode 4
    versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"

    buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""
    buildConfigField 'long', 'GIT_TIMESTAMP', "${gitTimestamp()}L"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
    }
  }

  sourceSets {
    main.java.srcDirs += "src/main/kotlin"
    test.java.srcDirs += "test/main/kotlin"
  }

  dexOptions {
    javaMaxHeapSize "2g"
  }
}

repositories {
  maven { url = "https://jitpack.io" }
}

ext {
  anvil_version = "0.4.0"
}

dependencies {
  compile fileTree(dir: "libs", include: ["*.jar"])

  testCompile "junit:junit:4.12"

  compile "com.facebook.stetho:stetho:1.3.1"
  compile "com.facebook.stetho:stetho-okhttp3:1.3.1"

  compile "com.android.support:appcompat-v7:25.1.0"
  compile "com.android.support:design:25.1.0"
  compile "com.android.support:recyclerview-v7:25.1.0"

  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  compile "co.trikita:anvil-sdk19:$anvil_version"
  compile "co.trikita:anvil-support-v4:$anvil_version"
  compile "co.trikita:anvil-appcompat-v7:$anvil_version"
  compile "co.trikita:anvil-design:$anvil_version"
  compile "co.trikita:anvil-recyclerview-v7:$anvil_version"

  compile "com.github.brianegan.bansa:bansa:1.0.0-beta"
  compile "com.github.brianegan.bansa:bansaKotlin:1.0.0-beta"
  debugCompile "com.github.brianegan.bansa:bansaDevTools:1.0.0-beta"
  debugCompile "com.github.brianegan.bansa:bansaDevToolsUi:1.0.0-beta"

  compile "com.github.andrewoma.dexx:kollection:0.6"

  debugCompile "com.squareup.leakcanary:leakcanary-android:1.4-beta2"

  compile "io.reactivex.rxjava2:rxjava:2.0.0-RC4"
}
